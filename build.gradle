apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'
apply plugin: 'checkstyle'
apply plugin: 'findbugs'
apply plugin: 'jacoco'
apply plugin: 'java-library-distribution'


// configuration of the generated JAR package
jar {
    manifest {
        attributes("Implementation-Title": "BACnet/IT Examples and Tests",
                   "Implementation-Version": version)
    }
}

// Maven repositories to look for dependencies
repositories {
    mavenLocal()
    jcenter()
}

// the project dependencies
dependencies {
    compile "ch.fhnw.bacnet-it:bacnet-it-ase:${ase_version}"
    compile "ch.fhnw.bacnet-it:bacnet-it-transport-binding-ws:${binding_ws_version}"
    compile "ch.fhnw.bacnet-it:bacnet-it-directory-binding-dnssd:${binding_dnssd_version}"
    compile 'org.slf4j:slf4j-api:1.7.24'
    testCompile 'junit:junit:4.12'
    testCompile 'ch.qos.logback:logback-classic:1.1.2'
    testCompile 'org.mockito:mockito-core:1.10.19'
    testCompile group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.4'
}

// dependencies of build task
build.dependsOn jacocoTestReport

// test tasks
test {
    // set this to false once stable
    ignoreFailures = false
    reports {
        junitXml.enabled = true
        html.enabled = true
    }               
}

checkstyle {
    toolVersion = '7.5.1'
}

findbugs {
    ignoreFailures = 'true'
}

jacocoTestReport {
    reports {
        xml.enabled = false
        csv.enabled = false
        html.enabled = true
    }
}

tasks.withType(Checkstyle) {
    reports {
        xml.enabled true
        html.enabled true
    }
}

tasks.withType(FindBugs) {
    reports {
        xml.enabled false
        html.enabled true
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    from javadoc.destinationDir
}

// install into Maven repository
publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = 'ch.fhnw.bacnet-it'
            artifactId = project.name
            version = project.version
            from components.java

            artifact sourcesJar {
                classifier = 'sources'
            }
            artifact javadocJar {
                classifier = 'javadoc'
            }
        }
    }
}

// make tar.gz
distTar {
    compression = Compression.GZIP
}

// set version of gradle wrapper to generate
wrapper.gradleVersion = '3.5'
